plugins {
	id("net.twisterrob.libraries.android.library")
}

android.namespace = "net.twisterrob.android"

repositories {
	maven { // jcenter() deprecated https://github.com/TWiStErRob/net.twisterrob.libraries/issues/5
		name = "jcenter"
		setUrl("https://jcenter.bintray.com/")
		content {
			includeModule("com.rarepebble", "colorpicker")
		}
	}
}

dependencies {
	implementation 'net.twisterrob.lib:twister-lib-general'
	implementation 'net.twisterrob.lib:twister-lib-android-defs'
	implementation 'net.twisterrob.lib:twister-lib-android-logging'
	implementation 'net.twisterrob.lib:twister-lib-android-basics'
	implementation 'net.twisterrob.lib:twister-lib-android-stringers'
	implementation 'net.twisterrob.lib:twister-lib-android-settings'

	implementation(libs.androidx.fragment)
	implementation(libs.androidx.cardview)
	implementation(libs.androidx.recyclerview)
	implementation(libs.androidx.material)
	implementation(libs.androidx.annotationExperimental)

	//implementation(libs.androidx.palette)

	// FIXME @Deprecated, use Glide
	implementation(libs.android.lrucache)

	// 2.4.+ is required for API 28+
	// because of HueSatView and SliderViewBase using removed Canvas.save(int) in earlier versions
	compileOnly(libs.android.colorpicker)

	testImplementation(project(":internal:test:android_unit"))
	// Replace APK's runtimeOnly sneaking into test runtime
	configurations.testRuntimeOnly.exclude module: 'twister-lib-android-slf4j'

	androidTestImplementation(libs.test.junit)
	androidTestImplementation(libs.test.params)
	androidTestImplementation "net.twisterrob.lib:twister-lib-hamcrest"
	androidTestImplementation "net.twisterrob.lib:twister-lib-mockito"
	androidTestImplementation(libs.test.android.mockito)
	androidTestImplementation "net.twisterrob.lib:twister-lib-junit4"
	androidTestImplementation "net.twisterrob.lib:twister-lib-test"
	androidTestImplementation(libs.test.hamcrest.junit)
	androidTestImplementation(libs.test.shazamcrest) { // assertThat(*, sameBeanAs(*))
		exclude group: 'junit'
		exclude module: 'guava'
		exclude group: 'org.json', module: 'json'
	}
	// Android JUnit Runner
	androidTestImplementation(libs.test.androidx.runner)
	androidTestImplementation(libs.test.androidx.junit)
	androidTestImplementation(libs.test.androidx.rules)

	// Espresso core
	androidTestImplementation(libs.test.androidx.espressoCore)
	// Espresso-contrib for DatePicker, RecyclerView, Drawer actions, Accessibility checks, CountingIdlingResource
	androidTestImplementation(libs.test.androidx.espressoContrib)
	// Make sure to bump the version coming transitively from Espresso to the one used by the project.
	// Resolves
	// > :processDebugAndroidTestManifest
	// > [androidx.vectordrawable:vectordrawable-animated:1.0.0]
	// > P:\caches\gradle\caches\transforms-2\files-2.1\54c0e3e3a1f93b71f3e9e8caba1f6095\vectordrawable-animated-1.0.0\AndroidManifest.xml
	// > Warning: Package name 'androidx.vectordrawable' used in: androidx.vectordrawable:vectordrawable-animated:1.0.0, androidx.vectordrawable:vectordrawable:1.0.0.
	androidTestImplementation(libs.androidx.material)
	// Espresso-accessibility for automatic checking (not used yet)
	// TODO AccessibilityChecks.enable()
	//androidTestImplementation(libs.test.androidx.espressoAccessibility)
	// disable web for now, because it has some conflicting Guava classes (https://issuetracker.google.com/issues/37063389)
	// Espresso-web for WebView support (not used yet)
	//androidTestImplementation(libs.test.androidx.espressoWeb)
	// Espresso-intents for capturing Intents and mocking onActivityResult
	androidTestImplementation(libs.test.androidx.espressoIntents)
	// Espresso-idling-resource for synchronization with background jobs
	androidTestImplementation(libs.test.androidx.espressoIdle)
	// CONSIDER using these with Glide
	//androidTestImplementation(libs.test.androidx.espressoConcurrent)
	//androidTestImplementation(libs.test.androidx.espressoNet)

	// UI Automator @SdkSuppress(minSdkVersion = VERSION_CODES.JELLY_BEAN_MR2)
	androidTestImplementation(libs.test.androidx.uiautomator)
}

android.lintOptions.checkDependencies = true
