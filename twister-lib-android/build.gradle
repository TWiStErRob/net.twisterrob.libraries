buildscript {
	repositories {
		//maven { name = "sonatype1"; setUrl("https://s01.oss.sonatype.org/service/local/repositories/nettwisterrob-1009/content/") }
		google()
		mavenCentral()
	}
	dependencies {
		configurations.named("classpath") { resolutionStrategy.cacheChangingModulesFor(0, "seconds") } // -SNAPSHOT
		classpath "net.twisterrob.gradle:twister-convention-plugins:${VERSION_TWISTER_GRADLE}"
		classpath "net.twisterrob.gradle:twister-quality:${VERSION_TWISTER_QUALITY}"
		classpath("com.android.tools.build:gradle:4.1.3")
	}
}

subprojects { Project project ->
	project.repositories {
		google()
		mavenCentral()
	}
	project.configurations.configureEach {
		if (it.name == 'lintClassPath') return
	}
	project.apply from: rootProject.file("gradle/substitutions.gradle"), to: project

	project.plugins.withId("net.twisterrob.android-library") {
		project.android.buildFeatures.buildConfig = false
		project.android.twisterrob.decorateBuildConfig = false
	}
	project.afterEvaluate {
		project.android.compileSdkVersion = 28
		project.android.defaultConfig.minSdkVersion = 14
		project.android.lintOptions.warningsAsErrors = true
		project.android.lintOptions.checkAllWarnings = true
		project.android.lintOptions.lintConfig = rootProject.file("config/lint/lint.xml")
		project.android.lintOptions.baseline rootProject.file("config/lint/lint-baseline-${project.name}.xml")

		//noinspection UnnecessaryQualifiedReference,GrDeprecatedAPIUsage REPORT cannot replace with new interface, missing methods
		com.android.build.gradle.api.AndroidSourceSet androidTest = project.android.sourceSets.androidTest
		if (androidTest.java.sourceFiles.empty) {
			logger.info "Disabling AndroidTest tasks in ${project.path} as it has no sources in ${androidTest.java.srcDirs}"
			project.tasks.configureEach { task ->
				if (task.name.contains("AndroidTest")) {
					task.enabled = false
				}
			}
		}
	}
}

tasks.register("clean", Delete.class) { clean ->
	clean.delete project.buildDir
}

tasks.register("cleanFull") { cleanFull ->
	// subprojects*.tasks*.named("clean") is not available at this point
	cleanFull.dependsOn(subprojects.collect { "${it.path}:clean" })
	cleanFull.dependsOn(tasks.named("clean"))
}

tasks.register("connectedCheck").configure {
	dependsOn(subprojects.collect { "${it.path}:connectedCheck" })
}

evaluationDependsOnChildren() // TODO somehow remove this: use build service as dex limiter in ColorFilters!
allprojects \
	.collectMany { it.tasks.withType(com.android.build.gradle.internal.tasks.DeviceProviderInstrumentTestTask) } \
	.inject { prev, curr -> curr.configure { it.mustRunAfter prev }; return prev }

apply plugin: 'net.twisterrob.quality'
